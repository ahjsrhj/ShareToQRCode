apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

Properties props = new Properties()
boolean isHasFile = false
File propFile = file('../signing.properties')
if (propFile.exists()) {
    isHasFile = true
    props.load(new FileInputStream(propFile))
}

def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "cn.imrhj.sharetoqrcode"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitCommitCount
        versionName "1.2.4"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        defaultConfig {
            keyAlias isHasFile ? props['ALIAS_NAME'] : System.getenv("ALIAS_NAME")
            keyPassword isHasFile ? props['ALIAS_PASS'] : System.getenv("ALIAS_PASS")
            storePassword isHasFile ? props['KEYSTORE_PASS'] : System.getenv("KEYSTORE_PASS")
            storeFile isHasFile ? file(props['KEYSTORE_URL']) : file("../.hexer.keystore")
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.defaultConfig
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.defaultConfig
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
}
